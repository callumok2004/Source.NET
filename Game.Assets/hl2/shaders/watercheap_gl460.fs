#version 460
// STATIC: "MULTITEXTURE"			"0..1"
// STATIC: "FRESNEL"				"0..1"
// STATIC: "BLEND"					"0..1"
// STATIC: "REFRACTALPHA"			"0..1"
// STATIC: "HDRTYPE"				"0..2"
// STATIC: "NORMAL_DECODE_MODE"		"0..0"

// DYNAMIC: "HDRENABLED"			"0..1"
// DYNAMIC: "PIXELFOGTYPE"			"0..1"

in vec2 vs_NormalMapTexCoord;
in vec3 vs_WorldVertToEyeVector;
in mat3 vs_TangentSpaceTranspose;
in vec4 vs_Refract_W_ProjZ;
#if MULTITEXTURE
in vec4 vs_ExtraBumpTexCoord;
#endif
in vec4 vs_FogFactorW;

layout(std140, binding = 6) uniform source_ps_constants {
    vec4 ps_const[256];
};

layout(std140, binding = 3) uniform source_pixel_sharedUBO {
    bool isAlphaTesting;
    int alphaTestFunc;
    float alphaTestRef;
};

out vec4 fragColor;

#include "common_gl460.fs"

#define g_WaterFogColor				ps_const[0].xyz
#define g_CheapWaterParams			ps_const[1]
#define g_ReflectTint				ps_const[2]
#define g_PixelFogParams			ps_const[3]

#define g_CheapWaterStart			g_CheapWaterParams.x
#define g_CheapWaterEnd				g_CheapWaterParams.y
#define g_CheapWaterDeltaRecip		g_CheapWaterParams.z
#define g_CheapWaterStartDivDelta	g_CheapWaterParams.w

#ifdef SOURCE_VULKAN
layout(set = 1, binding = 0) uniform textureCube EnvmapSampler_tex;
layout(set = 1, binding = 1) uniform sampler EnvmapSampler_smp;
#define EnvmapSampler samplerCube(EnvmapSampler_tex, EnvmapSampler_smp)
#else
layout(binding = 0) uniform samplerCube EnvmapSampler;
#endif
#ifdef SOURCE_VULKAN
layout(set = 1, binding = 2) uniform texture2D NormalMapSampler_tex;
layout(set = 1, binding = 3) uniform sampler NormalMapSampler_smp;
#define NormalMapSampler sampler2D(NormalMapSampler_tex, NormalMapSampler_smp)
#else
layout(binding = 1) uniform sampler2D NormalMapSampler;
#endif
#if REFRACTALPHA
#ifdef SOURCE_VULKAN
layout(set = 1, binding = 4) uniform texture2D RefractSampler_tex;
layout(set = 1, binding = 5) uniform sampler RefractSampler_smp;
#define RefractSampler sampler2D(RefractSampler_tex, RefractSampler_smp)
#else
layout(binding = 2) uniform sampler2D RefractSampler;
#endif
#endif
#ifdef SOURCE_VULKAN
layout(set = 1, binding = 12) uniform textureCube NormalizeSampler_tex;
layout(set = 1, binding = 13) uniform sampler NormalizeSampler_smp;
#define NormalizeSampler samplerCube(NormalizeSampler_tex, NormalizeSampler_smp)
#else
layout(binding = 6) uniform samplerCube NormalizeSampler;
#endif

void main()
{
    bool bBlend = BLEND != 0;

#if MULTITEXTURE
    vec3 vNormal  = texture(NormalMapSampler, vs_NormalMapTexCoord).xyz;
    vec3 vNormal1 = texture(NormalMapSampler, vs_ExtraBumpTexCoord.xy).xyz;
    vec3 vNormal2 = texture(NormalMapSampler, vs_ExtraBumpTexCoord.zw).xyz;
    vNormal = 0.33 * (vNormal + vNormal1 + vNormal2);

#if ( NORMAL_DECODE_MODE == NORM_DECODE_ATI2N )
    vNormal.xy = vNormal.xy * 2.0 - 1.0;
    vNormal.z = sqrt(1.0 - dot(vNormal.xy, vNormal.xy));
#else
    vNormal = 2.0 * vNormal - 1.0;
#endif

#else
    vec3 vNormal = DecompressNormal(TEX2D_ARG(NormalMapSampler), vs_NormalMapTexCoord, NORMAL_DECODE_MODE).xyz;
#endif

    vec3 worldSpaceNormal = vs_TangentSpaceTranspose * vNormal;
    vec3 worldSpaceEye;

    float flWorldSpaceDist = 1.0;

    // dimhotepus: Drop NV3X.
    if (bBlend)
    {
        worldSpaceEye = vs_WorldVertToEyeVector;
        flWorldSpaceDist = length(worldSpaceEye);
        worldSpaceEye /= flWorldSpaceDist;
    }
    else
    {
        worldSpaceEye = NormalizeWithCubemap(TEXCUBE_ARG(NormalizeSampler), vs_WorldVertToEyeVector);
    }

    vec3 reflectVect = CalcReflectionVectorUnnormalized(worldSpaceNormal, worldSpaceEye);
    vec3 specularLighting = ENV_MAP_SCALE * texture(EnvmapSampler, reflectVect).rgb;
    specularLighting *= g_ReflectTint.rgb;

#if FRESNEL
    // FIXME: It's unclear that we want to do this for cheap water
    // but the code did this previously and I didn't want to change it
    float flDotResult = dot(worldSpaceEye, worldSpaceNormal);
    flDotResult = 1.0 - max(0.0, flDotResult);

    float flFresnelFactor = flDotResult * flDotResult;
    flFresnelFactor *= flFresnelFactor;
    flFresnelFactor *= flDotResult;
#else
    float flFresnelFactor = g_ReflectTint.a;
#endif

    float flAlpha;
    if (bBlend)
    {
        float flReflectAmount = clamp(flWorldSpaceDist * g_CheapWaterDeltaRecip - g_CheapWaterStartDivDelta, 0.0, 1.0);
        flAlpha = clamp(flFresnelFactor + flReflectAmount, 0.0, 1.0);

#if REFRACTALPHA
        // Perform division by W only once
        float ooW = 1.0 / vs_Refract_W_ProjZ.z;
        vec2 unwarpedRefractTexCoord = vs_Refract_W_ProjZ.xy * ooW;
        float fogDepthValue = texture(RefractSampler, unwarpedRefractTexCoord).a;
        // Fade on the border between the water and land.
        flAlpha *= clamp((fogDepthValue - .05) * 20.0, 0.0, 1.0);
#endif
    }
    else
    {
        flAlpha = 1.0;
#if HDRTYPE == 0 || HDRENABLED == 0
        specularLighting = mix(g_WaterFogColor, specularLighting, flFresnelFactor);
#else
        specularLighting = mix(GammaToLinear(g_WaterFogColor), specularLighting, flFresnelFactor);
#endif
    }

    // multiply the color by alpha.since we are using alpha blending to blend against dest alpha for borders.



#if (PIXELFOGTYPE == PIXEL_FOG_TYPE_RANGE)
    float fogFactor = CalcRangeFog(vs_Refract_W_ProjZ.w, g_PixelFogParams.x, g_PixelFogParams.z, g_PixelFogParams.w);
#else
    float fogFactor = 0.0;
#endif

    fragColor = FinalOutput(vec4(specularLighting, flAlpha), fogFactor, PIXELFOGTYPE, TONEMAP_SCALE_LINEAR);
}
